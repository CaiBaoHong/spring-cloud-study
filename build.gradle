//引入spring boot gradle插件
buildscript {
    ext {
        springIOVersion = 'Brussels-SR6'
        springBootVersion = '1.5.9.RELEASE'
        springCloudVersion = 'Edgware.SR1'
    }
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public'}
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

//所有模块，含spring-cloud-study
allprojects{
    group 'com.abc'
    version '1.0'
}

//所有子模块，不含spring-cloud-study
subprojects {

    //应用插件
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    //编译设置
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    //设置maven阿里云镜像
    repositories {
        maven{ url 'http://maven.aliyun.com/nexus/content/groups/public'}
    }

    //设置后依赖不用写版本号
    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springIOVersion}"
            mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    //子模块公共的依赖，不包括spring-cloud-study模块
    dependencies {
        if(project.name != 'core') {
            compile project(':core')
        }
    }

}


